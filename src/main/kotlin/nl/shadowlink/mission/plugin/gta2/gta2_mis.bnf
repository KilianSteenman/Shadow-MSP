{
    parserClass="nl.shadowlink.mission.plugin.gta2.parser.Gta2MissionParser"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="Mission"
    psiImplClassSuffix="Impl"
    psiPackage="nl.shadowlink.mission.plugin.gta2.psi"
    psiImplPackage="nl.shadowlink.mission.plugin.gta2.psi.impl"

    elementTypeHolderClass="nl.shadowlink.mission.plugin.gta2.psi.Gta2MissionTypes"
    elementTypeClass="nl.shadowlink.mission.plugin.gta2.psi.Gta2MissionElementType"
    tokenTypeClass="nl.shadowlink.mission.plugin.gta2.psi.Gta2MissionTokenType"

  tokens=[
    SPACE='regexp:\s+'
    NUMBER='regexp:\d+(\.\d*)?'
    IDENTIFIER='regexp:[a-z0-9_]+'
    COMMENT='regexp://.*'
    SUBROUTINE='regexp:([a-z_]+):'
    COMMENTBLOCK='regexp:\/\*(\*(?!\/)|[^*])*\*\/'

    // Reserved words
    LEVEL_START='LEVELSTART'
    LEVEL_END='LEVELEND'
    MISSION_START='MISSIONSTART'
    MISSION_END='MISSIONEND'
    WHILE_EXEC = 'WHILE_EXEC'
    END_WHILE_EXEC = 'END_WHILE_EXEC'
    WHILE = 'WHILE'
    END_WHILE = 'ENDWHILE'
    IF = 'IF'
    END_IF = 'ENDIF'
    ELSE = 'ELSE'
    AND = 'AND'
    END = 'END'
    RETURN = 'RETURN'
    SET = 'SET'
    NOT = 'NOT'
    ON = 'ON'
    OFF = 'OFF'
    GOSUB = 'GOSUB'

    METHOD = 'regexp:[A-Z_]+'

    OP_PLUS_PLUS='++'
  ]
}

MissionFile ::= (DefinitionBlock? (LevelBlock | MissionBlock)?)

DefinitionBlock ::= Definition*

Definition ::= VariableDefinition | SubroutineDefinition | COMMENT | (Type SUBROUTINE)

VariableDefinition ::= (Type IDENTIFIER Assignment?)
SubroutineDefinition ::= SUBROUTINE Expression* RETURN

Type ::= PLAYER_PED | CHAR_DATA | COUNTER | FORWARD | TIMER_DATA | THREAD_TRIGGER | CAR_DATA

LevelBlock ::= (LEVEL_START LevelBody? LEVEL_END)
MissionBlock ::= (MISSION_START LevelBody? MISSION_END)
LevelBody ::= Expression*

private Assignment ::= '=' Param

Expression ::= COMMENT | VariableAssignment | MethodCall | WhileExecExpression | WhileExpression | IfExpression | SubroutineDefinition | SetExpression | MathAssignment | CommentBlock | SubroutineCall
VariableAssignment ::= IDENTIFIER '=' METHOD? Params
MethodCall ::= METHOD '(' Params ')' END?
SubroutineCall ::= GOSUB SUBROUTINE
MathAssignment ::= OP_PLUS_PLUS IDENTIFIER | IDENTIFIER OP_PLUS_PLUS
CommentBlock ::= COMMENTBLOCK
Params ::= (Param ','?)* ','?
Param ::= NUMBER | Enum | IDENTIFIER | SUBROUTINE | Vector3 | END | Boolean

Enum ::= MACHINE_GUN | CRIMINAL_TYPE2 | AREA_PLAYER_ONLY | REACT_AS_NORMAL | KILL_CHAR_ON_FOOT
Vector3 ::= '(' number ',' number ',' number ')'
Boolean ::= ON | OFF

ConditionalStatement ::= IDENTIFIER '=' Param | MethodCall | NOT? '(' ConditionalStatement ')' AndStatement*
AndStatement ::= AND '(' ConditionalStatement ')'

IfExpression ::= IF '(' ConditionalStatement ')' Expression* (ELSE Expression*)? END_IF
WhileExecExpression ::= WHILE_EXEC '(' ConditionalStatement ')' Expression* (END_WHILE_EXEC | END_WHILE)
WhileExpression ::= WHILE '(' ConditionalStatement ')' Expression* END_WHILE
SetExpression ::= (SET IDENTIFIER Assignment)