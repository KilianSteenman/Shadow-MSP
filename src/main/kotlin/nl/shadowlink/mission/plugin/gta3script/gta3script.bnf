{
    parserClass="nl.shadowlink.mission.plugin.gta3script.parser.Gta3ScriptParser"
    parserUtilClass="nl.shadowlink.mission.plugin.gta3script.parser.Gta3ScriptParserUtil"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="Gta3Script"
    psiImplClassSuffix="Impl"
    psiPackage="nl.shadowlink.mission.plugin.gta3script.psi"
    psiImplPackage="nl.shadowlink.mission.plugin.gta3script.psi.impl"

    elementTypeHolderClass="nl.shadowlink.mission.plugin.gta3script.psi.Gta3ScriptTypes"
    elementTypeClass="nl.shadowlink.mission.plugin.gta3script.psi.Gta3ScriptElementType"
    tokenTypeClass="nl.shadowlink.mission.plugin.gta3script.psi.Gta3ScriptTokenType"

    psiImplUtilClass="nl.shadowlink.mission.plugin.gta3script.psi.Gta3ScriptUtils"

  tokens=[
    SPACE='regexp:\s+'
    NUMBER='regexp:-?\d+(\.\d*)?'
    COMMENT='regexp://.*'
    COMMENTBLOCK='regexp:\/\*(\*(?!\/)|[^*])*\*\/'

    // Reserved words
    LEVEL_START='LEVELSTART'
    LEVEL_END='LEVELEND'
    MISSION_START='MISSION_START'
    MISSION_END='MISSION_END'
    WHILE_EXEC = 'WHILE_EXEC'
    END_WHILE_EXEC = 'END_WHILE_EXEC'
    WHILE = 'WHILE'
    END_WHILE = 'ENDWHILE'
    EXEC = 'EXEC'
    END_EXEC = 'ENDEXEC'
    IF = 'IF'
    END_IF = 'ENDIF'
    ELSE = 'ELSE'
    AND = 'AND'
    END = 'END'
    RETURN = 'RETURN'
    SET = 'SET'
    NOT = 'NOT'
    ON = 'ON'
    OFF = 'OFF'
    GOSUB = 'GOSUB'

    METHOD = 'regexp:[A-Z_]+'
    IDENTIFIER='regexp:\$?[a-zA-Z0-9_\.]+'

    OP_PLUS_PLUS='++'
    OP_PLUS='+'
    OP_MINUS_MINUS='--'
    OP_MINUS='-'
    OP_TIMES='*'
    OP_DIVISION='/'
    OP_LESS_THAN='<'
    OP_GREATER_THAN='>'
    EQUALS='='
  ]
}

ScriptFile ::= (MissionBlock? DefinitionBlock?)

MissionBlock ::= ('MISSION_START' MissionBody? 'MISSION_END')
MissionBody ::= Expression*

DefinitionBlock ::= Expression*

Definition ::= VariableDefinition | SubroutineDefinition | COMMENT | (Type IDENTIFIER)

VariableDefinition ::= (Type VariableIdentifierList) {
    mixin="nl.shadowlink.mission.plugin.gta3script.psi.Gta3ScriptNamedElementImpl"
    implements="nl.shadowlink.mission.plugin.gta3script.psi.Gta3ScriptNamedElement"
    methods=[getNameIdentifier setName getName]
}

VariableIdentifierList ::= (IDENTIFIER ','?)*

SubroutineDefinition ::= IDENTIFIER ':' Expression* RETURN {
    mixin="nl.shadowlink.mission.plugin.gta3script.psi.Gta3ScriptNamedElementImpl"
    implements="nl.shadowlink.mission.plugin.gta3script.psi.Gta3ScriptNamedElement"
    methods=[getNameIdentifier setName getName]
}

Type ::= VAR_INT | VAR_FLOAT

Expression ::= COMMENT | VariableAssignment | MethodCall | WhileExpression | IfExpression | SubroutineDefinition | SetExpression | MathAssignment | CommentBlock | GosubCall | Definition | ExecExpression | LocalScope
LocalScope ::= '{' Expression* '}'
VariableAssignment ::= VariableReference EQUALS Param ((MathOperator Param)+)?
MethodCall ::= METHOD Params
GosubCall ::= GOSUB SubroutineReference
SubroutineReference ::= IDENTIFIER {
    methods=[getReference]
}
VariableReference ::= IDENTIFIER {
    methods=[getReference]
}
MathAssignment ::= OP_PLUS_PLUS VariableReference | VariableReference OP_PLUS_PLUS
CommentBlock ::= COMMENTBLOCK
Params ::= (Param ','?)* ','?
Param ::= NUMBER | VariableReference | SUBROUTINE | Boolean

Boolean ::= ON | OFF

ConditionalStatement ::= NOT? MethodCall

IfExpression ::= IF ConditionalStatement Expression* END_IF
WhileExpression ::= WHILE NOT? ConditionalStatement ((OR | AND) NOT? Expression)* Expression* END_WHILE
ExecExpression ::= EXEC Expression* END_EXEC
SetExpression ::= SET VariableReference EQUALS (Param | MathOperation)

MathOperation ::= '(' (NUMBER | IDENTIFIER) MathOperator (NUMBER | IDENTIFIER) ')'
MathOperator ::= OP_PLUS | OP_MINUS | OP_TIMES | OP_DIVISION